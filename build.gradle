task clean(type: Delete) {
    delete rootProject.buildDir
}

tasks.register('saveLintResultsForSlack') {
    ext.lintFile = file('app/build/reports/lint-results-release.xml')

    doLast {
        def output = new File(projectDir, "lint_results_for_slack.txt")
        def content = new StringBuffer()
        def issues = new XmlParser().parse(lintFile)
        issues.each { issue ->
            content.append(
                String.format(
                    '⚠️  %s [%s] ⚠️\n' +
                    ' > %s\n' +
                    ' > %s\n' +
                    ' → %s@%s%s\n\n',
                    issue.attribute("message"),
                    issue.attribute("id"),
                    issue.attribute("errorLine1"),
                    issue.attribute("errorLine2"),
                    issue.location[0].attribute("file").replace(output.getParent()+'/', ''),
                    issue.location[0].attribute("line"),
                    issue.location[0].attribute("column")
                )
            )
        }
        output.text = content.toString()
    }
}

